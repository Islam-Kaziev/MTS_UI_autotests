plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group 'guru.ea'
version '1.0-SNAPSHOT'

compileJava {
    options.encoding = 'UTF-8'
}
compileTestJava {
    options.encoding = 'UTF-8'
}

allure {
    report {
        version.set("2.21.0")
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set("2.21.0") //версия интеграции фреймворка и Allure
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(
            'com.github.javafaker:javafaker:1.0.2',
            'org.junit.jupiter:junit-jupiter:5.9.2',
            'com.codeborne:selenide:6.12.3',
            'io.qameta.allure:allure-selenide:2.21.0',
            'org.aeonbits.owner:owner:1.0.4'
    )
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task simple_test(type: Test) {
    useJUnitPlatform {
        includeTags("simple")
    }
}

task one_property_test(type: Test) {
    useJUnitPlatform {
        includeTags("one_property")
    }
}